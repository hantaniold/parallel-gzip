# Configure template for gzip.

# Copyright (C) 1999, 2000, 2001, 2002 Free Software Foundation, Inc.
# Copyright (C) 1992-1993 Jean-loup Gailly

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

AC_INIT(gzip.c)
AM_INIT_AUTOMAKE(gzip, 1.3.3)
AM_CONFIG_HEADER(config.h)
AC_PREREQ(2.53)
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_SHELL
AC_OBJEXT
AC_AIX
AC_MINIX
AC_SYS_LARGEFILE

dnl
dnl Try to assemble match.S with and without leading underline.
dnl cc -E produces incorrect asm files on SVR4, we must use /lib/cpp.
dnl Also, "gcc -E match.s" ignores -E, so we must use match.S.
echo checking for underline in external names
test -z "$ASCPP" -a -f /lib/cpp && ASCPP=/lib/cpp
test -z "$ASCPP" && ASCPP="$CC -E"
cat > conftest.c <<EOF
int foo() {return 0;}
EOF
eval "$CC -c conftest.c > /dev/null 2>&1"
if nm conftest.$OBJEXT | grep _foo > /dev/null 2>&1 ; then
  :
else
  ASCPP="${ASCPP} -DNO_UNDERLINE"
fi
rm -f _match.$OBJEXT conftest.c conftest.$OBJEXT
if echo "$DEFS" | grep NO_ASM >/dev/null; then
  :
else
  echo checking for assembler
  if eval "$ASCPP $srcdir/match.S > _match.s 2>/dev/null"; then
    if test ! -s _match.s || grep error < _match.s > /dev/null; then
      :
    elif eval "$CC -c _match.s >/dev/null 2>&1" && test -f _match.$OBJEXT; then
      AC_DEFINE(ASMV, ,
	[Define if an assembler version of longest_match is available.])
      AC_LIBOBJ(match)
    fi
  fi
  rm -f _match.s _match.$OBJEXT
fi
dnl
AC_EXEEXT
AC_ISC_POSIX
AC_C_CONST
AC_STDC_HEADERS
AC_CHECK_HEADERS(fcntl.h limits.h memory.h \
  stdlib.h string.h sys/utime.h time.h unistd.h utime.h)
AC_CHECK_FUNCS(lstat utime)
AC_REPLACE_FUNCS(rpmatch)
AC_HEADER_DIRENT
AC_TYPE_SIGNAL
AC_TYPE_SIZE_T
AC_TYPE_OFF_T
AC_SYS_INTERPRETER
case $interpval in
yes) SEDCMD='1d';;
*) SEDCMD='';;
esac

AC_PREFIX_PROGRAM(gzip)
AC_SUBST(CFLAGS)dnl
AC_SUBST(ASCPP)dnl
AC_SUBST(SEDCMD)dnl
AC_OUTPUT(Makefile)
